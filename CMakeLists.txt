cmake_minimum_required(VERSION 3.10)
project(labyrinth CXX)

set(CMAKE_CXX_STANDARD 17)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")
include(SDL2)

if(UNIX)
    include(FindPkgConfig)
    pkg_check_modules(SDL2 REQUIRED IMPORTED_TARGET sdl2)
    pkg_check_modules(SDL2_image REQUIRED IMPORTED_TARGET SDL2_image)
    pkg_check_modules(SDL2_net REQUIRED IMPORTED_TARGET SDL2_net)
    pkg_check_modules(SDL2_mixer REQUIRED IMPORTED_TARGET SDL2_mixer)
    pkg_check_modules(SDL2_ttf REQUIRED IMPORTED_TARGET SDL2_ttf)
else()
    set(SDL2_ROOT "${CMAKE_SOURCE_DIR}" CACHE PATH "Root directory to SDL2 development packages")
    set(SDL2_NET_ROOT "${CMAKE_SOURCE_DIR}" CACHE PATH "Root directory to SDL2_net development packages")
    set(SDL2_IMAGE_ROOT "${CMAKE_SOURCE_DIR}" CACHE PATH "Root directory to SDL2_image development packages")
    set(SDL2_TTF_ROOT "${CMAKE_SOURCE_DIR}" CACHE PATH "Root directory to SDL2_ttf development packages")
    set(SDL2_MIXER_ROOT "${CMAKE_SOURCE_DIR}" CACHE PATH "Root directory to SDL2_mixer development packages")

    add_sdl_library(SDL2 ${SDL2_ROOT} SDL2.dll)
    add_sdl_library(SDL2main ${SDL2_ROOT})
    add_sdl_library(SDL2_ttf ${SDL2_TTF_ROOT} libfreetype-6.dll SDL2_ttf.dll zlib1.dll)
    add_sdl_library(SDL2_mixer ${SDL2_MIXER_ROOT} libogg-0.dll libvorbis-0.dll libvorbisfile-3.dll SDL2_mixer.dll)
    add_sdl_library(SDL2_net ${SDL2_NET_ROOT} SDL2_net.dll)
    add_sdl_library(SDL2_image ${SDL2_IMAGE_ROOT} libpng16-16.dll SDL2_image.dll)
endif()

set(ASSETS_DIRECTORY "${CMAKE_SOURCE_DIR}/assets" CACHE PATH "Directory for game assets")

add_subdirectory(common)
add_subdirectory(server)
add_subdirectory(client)
